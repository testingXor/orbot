apply from: "../commons-device.gradle"
apply plugin: 'kotlin-android'
apply from : '../dependencies.gradle'

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}

android {
    defaultConfig {
        applicationId "org.torproject.android"
        versionName getVersionName()
        minSdkVersion 16
        compileSdkVersion 31
        targetSdkVersion 31
        multiDexEnabled true
    }
    compileOptions {
//        coreLibraryDesugaringEnabled true
    }
    lintOptions {
        htmlReport true
        xmlReport false
        textReport false
        lintConfig file("../lint.xml")
        disable "InvalidPackage"
    }
    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
        }
    }

    flavorDimensions "free"

    productFlavors {
        fullperm {
            dimension "free"

            minSdkVersion 16
            applicationId 'org.torproject.android'
            targetSdkVersion 31
            versionCode 1700100400

            versionName getVersionName()
            archivesBaseName = "Orbot-$versionName"
        }

        nightly {
            dimension "free"

            minSdkVersion 16
            applicationId 'org.torproject.android.nightly'
            targetSdkVersion 31
            versionName getVersionName()
            versionCode versionCodeEpoch()
            archivesBaseName = "Orbot-Nightly-$versionName"
        }
    }

    splits {
        abi {
            universalApk true
        }
    }

    dependencies {
        implementation(
                project(':appcore'),
                project(':intentintegrator'),
                project(':orbotservice'),
                libs.android_material,
                libs.android_volley,
                libs.androidx_localbroadcast,
                libs.ipt_proxy
        )
        implementation "com.squareup.retrofit2:retrofit:2.9.0"
        implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

        androidTestImplementation libs.fastlane_screengrab
//        coreLibraryDesugaring 'com.android.tools:desugar_j dk_libs:1.1.5'

        //building from AAR built from https://github.com/guardianproject/tor-android/commit/95eca2a8c9eb44068f11332d7731580a13ef7b28
        //    until a new full release is done
       implementation files('../libs/tor-android-binary-release.aar')

    }
}
dependencies {
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
